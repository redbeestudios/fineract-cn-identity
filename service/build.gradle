/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath ("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id("org.nosphere.apache.rat") version "0.3.1"
    id "com.jfrog.artifactory" version "4.9.5"
}

apply from: '../shared.gradle'

apply plugin: 'spring-boot'
apply plugin: 'docker'

springBoot {
    executable = true
    classifier = 'boot'
}

dependencies {
    compile(
            [group: 'org.springframework.cloud', name: 'spring-cloud-starter-config'],
            [group: 'org.springframework.cloud', name: 'spring-cloud-starter-eureka'],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'],
            [group: 'org.hibernate', name: 'hibernate-validator', version: versions.hibernatevalidator],
            [group: 'org.apache.fineract.cn', name: 'lang', version: versions.frameworklang],
            [group: 'org.apache.fineract.cn', name: 'async', version: versions.frameworkasync],
            [group: 'org.apache.fineract.cn', name: 'cassandra', version: versions.frameworkcassandra],
            [group: 'io.jsonwebtoken', name: 'jjwt', version: versions.jjwt],
            [group: 'org.apache.fineract.cn', name: 'crypto', version: versions.frameworkcrypto],
            [group: 'org.apache.fineract.cn.identity', name: 'api', version: rootProject.version],
            [group: 'org.apache.fineract.cn.anubis', name: 'api', version: versions.frameworkanubis],
            [group: 'org.apache.fineract.cn.anubis', name: 'library', version: versions.frameworkanubis],
    )

    // redbee added - compatibility with kafka client in command jar new version
    compile ('org.apache.kafka:kafka-clients:0.11.0.2') {
        force true
    }

    compile ('org.springframework:spring-messaging:4.3.3.RELEASE') {
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
    }

    compile (group: 'org.apache.fineract.cn', name: 'command', version: versions.frameworkcommand)
}

publishToMavenLocal.dependsOn bootRepackage


publishing {
    publications {
        service(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            def repo = version.endsWith('SNAPSHOT') ? 'waas-snapshot' : 'waas'
            url "http://registry-nd.dev.redbee.io/repository/${repo}"

            credentials {
                username = String.valueOf(System.getenv("NEXUS_USER"))
                password = String.valueOf(System.getenv("NEXUS_PASS"))
            }

        }
    }
}


def dockerImgName = 'fineract-cn-identity'


////START Build docker image
if (!project.hasProperty("dockerImgName")) {
    ext.dockerImgName = "${dockerImgName}"
}
if (!project.hasProperty("dockerTag")) {
    ext.dockerTag = "latest"
}
if (!project.hasProperty("dockerRegistry")) {
    ext.dockerRegistry = "registry.dev.redbee.io"
}
if (!project.hasProperty("dockerPushToRegistry")) {
    ext.dockerPushToRegistry = true
}
task buildDocker(type: Docker, dependsOn: build) {
    baseImage = "openjdk:8-jdk-alpine"
    maintainer = "vbees"
    hostUrl = "https://registry.dev.redbee.io"
    push = dockerPushToRegistry.toBoolean()
    dryRun = false
    tagVersion = "${dockerTag}"
    tag = "${dockerRegistry}/${dockerImgName}"
    addFile {
        from bootRepackage
        rename { "${dockerImgName}.jar" }
    }
    addFile("./java-entrypoint.sh", "/java-entrypoint.sh")
    //exposePort(8080)

    entryPoint([
        "/java-entrypoint.sh",
        "/${dockerImgName}.jar"
    ])
}
